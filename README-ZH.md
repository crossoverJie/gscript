<div align="center">  


```
 _     _   
 ___ ___ ___ ___|_|___| |_ 
| . |_ -|  _|  _| | . |  _|
|_  |___|___|_| |_|  _|_|  
|___|             |_|   

```

ğŸ“˜[ç‰¹æ€§](#ç‰¹æ€§) | ğŸŒ°[ä¾‹å­](#ä¾‹å­) | ğŸ‘¾[REPL](#repl) | ğŸ‰[è¯­æ³•](#è¯­æ³•) | ğŸ[æ ‡å‡†åº“](#æ ‡å‡†åº“) | ğŸ”§[å®‰è£…](https://github.com/crossoverJie/gscript/releases) | ğŸ’¡[è”ç³»ä½œè€…](#è”ç³»ä½œè€…) | ğŸ‡¦ğŸ‡º[è‹±æ–‡æ–‡æ¡£](https://github.com/crossoverjie/gscript/blob/master/README.md)


</div><br>


# ä»‹ç»

è¿™æ˜¯ä¸€é—¨ç”¨ Go ç¼–å†™çš„ä¸€æ¬¾**é™æ€ã€å¼ºç±»å‹**çš„è„šæœ¬è¯­è¨€ï¼Œå¤§éƒ¨åˆ†è¯­æ³•å‚è€ƒäº† `Java` ä»¥åŠå°‘é‡çš„ `Go`ã€‚

> å½“å‰ç‰ˆæœ¬ä»…ä¾›å­¦ä¹ ä¸å®éªŒã€‚

è¿è¡Œï¼š

hello_world.gs:
```js
println("hello world");
```

```shell
â¯ gscript hello_world.gs
hello world
```

# ç‰¹æ€§
- [x] [classå£°æ˜](#class)
- [x] [å‡½æ•°å£°æ˜ä¸è°ƒç”¨](#å‡½æ•°)
- [x] [åŸºæœ¬ç±»å‹](#åŸºæœ¬ç±»å‹): `int/string/float/bool`
- [x] [arrayæ•°ç»„ç±»å‹](#æ•°ç»„)
- [x] `any` [é€šç”¨ç±»å‹](#any)
- [x] ç‰¹æ®Šç±»å‹ `nil`
- [x] å‡½æ•°ç±»å‹
- [x] [é—­åŒ…ï¼šå‡½æ•°ä¸€ç­‰å…¬æ°‘](#é—­åŒ…)
- [x] [å†…ç½®å‡½æ•°](#å†…ç½®å‡½æ•°): `len()/hash()/assertEqual()/JSON()/JSONGet()`
- [x] [æ ‡å‡†åº“](#æ ‡å‡†åº“)
	- [x] [Map](#map)
- [x] [å¯å˜å‚æ•°](#å¯å˜å‚æ•°)
- [x] [è¿ç®—ç¬¦é‡è½½](#è¿ç®—ç¬¦é‡è½½)
- [x] [åŸç”Ÿ `json` æ”¯æŒ](#å†…ç½®å‡½æ•°)
- [x] [åŸç”Ÿ `http` åŒ…æ”¯æŒ](#http)
- [x] æ¡ˆä¾‹
	- [x] LeetCode
		- [x] [åˆ¤æ–­é“¾è¡¨æ˜¯å¦æœ‰ç¯ ](https://github.com/crossoverJie/gscript/blob/main/example/linked_list_cycle141.gs)
		- [x] [ä¸¤æ•°ä¹‹å’Œ](https://github.com/crossoverJie/gscript/blob/main/example/leetcode/two_sum.gs)
	- [x] [æ‰“å°æ–æ³¢é‚£å¥‘æ•°åˆ— ](#æ‰“å°æ–æ³¢é‚£å¥‘æ•°åˆ—)
	- [x] [æ‰“å°æ¨è¾‰ä¸‰è§’ ](#æ¨è¾‰ä¸‰è§’)
	- [x] [HTTP Service](https://github.com/crossoverJie/gscript/blob/main/example/http_server.gs)
-  [ ] åŒ…ç®¡ç†
-  [ ] å•æµ‹å‘½ä»¤è¡Œå·¥å…·

# ä¾‹å­

## Hello world
```js
println("hello world");
```

## æ‰“å°æ–æ³¢é‚£å¥‘æ•°åˆ—

```js
func int() fib(){
    int a = 0;
    int b = 1;
    int fibonacci(){
        int c = a;
        a = b;
        b = a+c;
        return c;
    }
    return fibonacci;
}

func int() f = fib();

for (int i = 0; i < 10; i++){
    println(f());
}
```

## ç½‘ç«™åº”ç”¨

è¿™æ˜¯ç”¨ `GScript` ç¼–å†™çš„åŠ¨æ€ç½‘é¡µï¼š

[https://gscript.crossoverjie.top/index](https://gscript.crossoverjie.top/index)

æºç åœ°å€ï¼š
[https://github.com/crossoverjie/gscript-homepage](https://github.com/crossoverjie/gscript-homepage)

## æ¨è¾‰ä¸‰è§’

```js
int num(int x,int y){
	if (y==1 || y ==x) {
		return 1;
	}
    int v1 = num(x - 1, y - 1);
    int v2 = num(x - 1, y);
	int c = v1+v2;
    // int c = num(x - 1, y - 1)+num(x - 1, y);
	return c;
}

printTriangle(int row){
	for (int i = 1; i <= row; i++) {
        for (int j = 1; j <= row - i; j++) {
           print(" ");
        }
        for (int j = 1; j <= i; j++) {
            print(num(i, j) + " ");
        }
        println("");
    }
}

printTriangle(7);

// output:
      1 
     1 1 
    1 2 1 
   1 3 3 1 
  1 4 6 4 1 
 1 5 10 10 5 1 
1 6 15 20 15 6 1
```

---

æ›´å¤šä¾‹å­ï¼š[https://github.com/crossoverJie/gscript/tree/main/example](https://github.com/crossoverJie/gscript/tree/main/example)

# REPL
```shell
> ./gscript 
```

![](doc/repl.gif)

# è¯­æ³•

## åŸºæœ¬ç±»å‹

å½“å‰ç‰ˆæœ¬æ”¯æŒ `int/string/float/bool` å››ç§åŸºæœ¬ç±»å‹ä»¥åŠ `nil` ç‰¹æ®Šç±»å‹ã€‚

å˜é‡å£°æ˜è¯­æ³•ï¼š`type identifier (= expr)?`ã€‚

```js
int a=10;
string b,c;
float e = 10.1;
bool f = false;
string x = ^
{
    "name": "bob",
    "age": 20,
    "skill": {
        "lang": [
            {
                "go": {
                    "feature": [
                        "goroutine",
                        true
                    ]
                }
            }
        ]
    }
}^;
```

## æ•°ç»„
æ•°ç»„å£°æ˜è¯­æ³•ï¼š`('[' DECIMAL_LITERAL ']')? '{' (variableInitializer (',' variableInitializer)* (',')? )? '}'`
```js
// å£°æ˜å¹¶åˆå§‹åŒ–
int[] a={1,2,3};
println(a);

// å£°æ˜ä¸€ä¸ªç©ºæ•°ç»„å¹¶æŒ‡å®šå¤§å°
int[] table = [4]{};

println("");
// å‘æ•°ç»„ append æ•°æ®
a = append(a,4);
println(a);
for(int i=0;i<len(a);i++){
	println(a[i]);
}

// é€šè¿‡ä¸‹æ ‡è·å–æ•°ç»„æ•°æ®
int b=a[2];
println(b);
```

## any

`any` æ˜¯é€šç”¨ç±»å‹ï¼Œç±»ä¼¼äº Java ä¸­çš„ Object å’Œ Go ä¸­çš„ `interface{}`ã€‚

```js
any a =10;
println(a);

int fun1(any a,int b){
	return a+b;
}
int v =fun1(1,2);
println(v);
assertEqual(v,3);

any v2 = fun1(1,2);
println(v2);
assertEqual(v2,3);

int fun2(int a, any b){
	return a+b;
}
int v3 =fun2(1,2);
println(v3);
assertEqual(v3,3);

int fun3(any a, any b){
	return a+b;
}
int v4 =fun3(1,2);
println(v4);
assertEqual(v4,3);

int fun4(any a, any b){
	return a+b;
}
string v5 =fun4("10", "20");
println(v5);
assertEqual(v5,"1020");
```

[æ ‡å‡†åº“](https://github.com/crossoverJie/gscript/blob/main/internal/internal.gs#L25)ä¸­æœ‰ç›¸å…³åº”ç”¨ã€‚
## Class

è‡ªå®šä¹‰ Class ä¸ Java ç±»ä¼¼ï¼š

```js
class ListNode{
    int value;
    ListNode next;
    ListNode(int v, ListNode n){
        value =v;
        next = n;
    }
}

// è°ƒç”¨æ„é€ å‡½æ•°æ—¶ä¸éœ€è¦ä½¿ç”¨ new å…³é”®å­—ã€‚
ListNode l1 = ListNode(1, nil);

// ä½¿ç”¨ . è°ƒç”¨å¯¹è±¡å±æ€§æˆ–å‡½æ•°ã€‚
println(l1.value);
```

ç¼ºçœæƒ…å†µä¸‹ `class` å…·æœ‰æ— å‚æ„é€ å‡½æ•°ï¼š

```js
class Person{
	int age=10;
	string name="abc";
	int getAge(){
		return 100+age;
	}
}

// æ— å‚æ„é€ å‡½æ•°
Person xx= Person();
println(xx.age);
assertEqual(xx.age, 10);
println(xx.getAge());
assertEqual(xx.getAge(), 110);
```


## å‡½æ•°

```js
// åˆ¤æ–­é“¾è¡¨æ˜¯å¦æœ‰ç¯
bool hasCycle(ListNode head){
    if (head == nil){
        return false;
    }
    if (head.next == nil){
        return false;
    }

    ListNode fast = head.next;
    ListNode slow = head;
    bool ret = false;
    for (fast.next != nil){
        if (fast.next == nil){
            return false;
        }
        if (fast.next.next == nil){
            return false;
        }
        if (slow.next == nil){
            return false;
        }
        if (fast == slow){
            ret = true;
            return true;
        }

        fast = fast.next.next;
        slow = slow.next;
    }
    return ret;
}

ListNode l1 = ListNode(1, nil);
bool b1 =hasCycle(l1);
println(b1);
assertEqual(b1, false);

ListNode l4 = ListNode(4, nil);
ListNode l3 = ListNode(3, l4);
ListNode l2 = ListNode(2, l3);
bool b2 = hasCycle(l2);
println(b2);
assertEqual(b2, false);

l4.next = l2;
bool b3 = hasCycle(l2);
println(b3);
assertEqual(b3, true);
```

å‡½æ•°å£°æ˜è¯­æ³•ï¼š`typeTypeOrVoid? IDENTIFIER formalParameters ('[' ']')*`

```js
add(int a){}
```

> å½“å‡½æ•°æ²¡æœ‰è¿”å›å€¼æ—¶ï¼Œå¯ä»¥å£°æ˜ä¸º void æˆ–ç›´æ¥å¿½ç•¥è¿”å›ç±»å‹ã€‚


## é—­åŒ…

åœ¨ `GScript` ä¸­ï¼Œå‡½æ•°ä½œä¸ºä¸€ç­‰å…¬æ°‘å¯ä»¥ä½œä¸ºå˜é‡ä¼ é€’ï¼ŒåŒæ—¶ä¹Ÿèƒ½å®ç°é—­åŒ…ã€‚

å‡½æ•°ç±»å‹è¯­æ³•ï¼š`func typeTypeOrVoid '(' typeList? ')'`

```js
// å¤–éƒ¨å˜é‡ï¼Œå…¨å±€å…±äº«ã€‚
int varExternal =10;
func int(int) f1(){
	// é—­åŒ…å˜é‡å¯¹æ¯ä¸ªé—­åŒ…å•ç‹¬å¯è§
	int varInner = 20;
	int innerFun(int a){
		println(a);
		int c=100;
		varExternal++;
		varInner++;
		return varInner;
	}
	return innerFun;
}

// f2 ä½œä¸ºä¸€ä¸ªå‡½æ•°ç±»å‹ï¼Œæ¥æ”¶çš„æ˜¯ä¸€ä¸ªè¿”å›å€¼å’Œå‚æ•°éƒ½æ˜¯ int çš„å‡½æ•°ã€‚
func int(int) f2 = f1();
for(int i=0;i<2;i++){
	println("varInner=" + f2(i) + ", varExternal=" + varExternal);
}
println("=======");
func int(int) f3 = f1();
for(int i=0;i<2;i++){
	println("varInner=" + f3(i) + ", varExternal=" + varExternal);
}
```

æœ€ç»ˆè¾“å‡ºå¦‚ä¸‹ï¼š

```shell
0
varInner=21, varExternal=11
1
varInner=22, varExternal=12
=======
0
varInner=21, varExternal=13
1
varInner=22, varExternal=14

```

## å¯å˜å‚æ•°

`GScript` æ”¯æŒå‡½æ•°å®šä¹‰å¯å˜å‚æ•°ï¼Œè¯­æ³•å¦‚ä¸‹ï¼š

```js
int add(string s, int ...num){
	println(s);
	int sum = 0;
	for(int i=0;i<len(num);i++){
		int v = num[i];
		sum = sum+v;
	}
	return sum;
}
int x = add("abc", 1,2,3,4);
println(x);
assertEqual(x, 10);
```


## è¿ç®—ç¬¦é‡è½½
`GScript` æ”¯æŒä»¥ä¸‹è¿ç®—ç¬¦é‡è½½ï¼š
- `+-*/`
- `== != < <= > >=`

> é‡è½½å‡½æ•°åç§°å¿…é¡»æ˜¯ **operator**ï¼Œåç§°åè·Ÿä¸Šè¿ç®—ç¬¦å³å¯é‡è½½ã€‚

```js
class Person{
	int age;
	Person(int a){
		age = a;
	}
}
Person operator + (Person p1, Person p2){
	Person pp = Person(p1.age+p2.age);
	return pp;
}
Person operator - (Person p1, Person p2){
	Person pp = Person(p1.age-p2.age);
	return pp;
}
Person operator * (Person p1, Person p2){
	Person pp = Person(p1.age * p2.age);
	return pp;
}
Person operator / (Person p1, Person p2){
	Person pp = Person(p1.age / p2.age);
	return pp;
}
bool operator == (Person p1, Person p2){
	return p1.age==p2.age;
}
bool operator != (Person p1, Person p2){
	return p1.age!=p2.age;
}
bool operator > (Person p1, Person p2){
	return p1.age>p2.age;
}
bool operator >= (Person p1, Person p2){
	return p1.age>=p2.age;
}
bool operator < (Person p1, Person p2){
	return p1.age<p2.age;
}
bool operator <= (Person p1, Person p2){
	return p1.age<=p2.age;
}
Person p1 = Person(10);
Person p2 = Person(20);
//Person p3 =  operator(p1,p2);
Person p3 = p1+p2;
println("p3.age="+p3.age);
assertEqual(p3.age, 30);

Person p4 = p1-p2;
println("p4.age="+p4.age);
println(100-10);

Person p5 = p1*p2;
println("p5.age="+p5.age);
assertEqual(p5.age, 200);

Person p6 = p2/p1;
println("p6.age="+p6.age);
assertEqual(p6.age, 2);

bool b1 = p1 == p2;
println("b1=="+b1);
assertEqual(b1,false);

bool b2 = p1 != p2;
println("b2=="+b2);
assertEqual(b2,true);

bool b3 = p1 > p2;
println("b3=="+b3);
assertEqual(b3,false);

bool b4 = p1 >= p2;
println("b4=="+b4);
assertEqual(b4,false);

bool b5 = p1 < p2;
println("b5=="+b5);
assertEqual(b5,true);

bool b6 = p1 <= p2;
println("b6=="+b6);
assertEqual(b6,true);
```


æ›´å¤šæ ·ä¾‹è¯·å‚è€ƒï¼š[https://github.com/crossoverJie/gscript/tree/main/example](https://github.com/crossoverJie/gscript/tree/main/example)

# æ ‡å‡†åº“

æ ‡å‡†åº“æºç ï¼š[https://github.com/crossoverJie/gscript/tree/main/internal](https://github.com/crossoverJie/gscript/tree/main/internal)

## å†…ç½®å‡½æ•°

```js

printf("hello %s ","123");
printf("hello-%s-%s ","123","abc");
printf("hello-%s-%d ","123",123);

string s = sprintf("nice to meet %s", "you");
println(s);
assertEqual(s,"nice to meet you");

int[] a={1,2,3};
// len è¿”å›æ•°ç»„å¤§å°
println(len(a));

// å‘æ•°ç»„è¿½åŠ æ•°æ®
append(a,4);
println(a);
// output: [1,2,3,4]

// æ–­è¨€å‡½æ•°ï¼Œä¸ç›¸ç­‰æ—¶ä¼šæŠ›å‡ºè¿è¡Œæ—¶å¼‚å¸¸ï¼Œå¹¶ä¸­æ–­ç¨‹åºã€‚
assertEqual(len(a),4);

// è¿”å› hashcode
int hashcode = hash(key);

// åºåˆ—åŒ– JSON å­—ç¬¦ä¸²
class P{
	string name;
	P(string n){
		name = n;
	}
}
class Object{
	P p;
	int x;
	Object(P pp, int xx){
		p = pp;
		x = xx;
	}
}
P p1 = P("abc");
Object o1 = Object(p1, 100);
string json = JSON(o1);
println(json); //{"p":{"name":"abc"},"x":100}

// æŸ¥è¯¢ JSON
int x = JSONGet(json,"x");
println(x);
assertEqual(x,100);

string name = JSONGet(json,"p.name");
println(name);
assertEqual(name,"abc");

// è·å–å¯åŠ¨å‚æ•°
System s = System();
string[] args = s.getOSArgs();
```



> æ›´å¤š JSON æŸ¥è¯¢è¯­æ³•è¯·å‚è€ƒï¼š[xjson](https://github.com/crossoverJie/xjson#arithmetic-syntax)

## Map
å‡½æ•°å®šä¹‰ï¼š
```js
class Map{
	put(any key, any value){}
	any get(any key){}
}
```

```js
int count =100;
Map m1 = Map();
for (int i=0;i<count;i++){
	string key = i+"";
	string value = key;
	m1.put(key,value);
}
println(m1.getSize());
assertEqual(m1.getSize(),count);

for (int i=0;i<count;i++){
	string key = i+"";
	string value = m1.get(key);
	println("key="+key+ ":"+ value);
	assertEqual(key,value);
}
```

# http
æ ‡å‡†åº“å®šä¹‰ï¼š

```js
// http lib
// Response json
FprintfJSON(int code, string path, string json){}
// Resonse html
FprintfHTML(int code, string path, string html){}

// path (relative paths may omit leading slash)
string QueryPath(string path){}

string FormValue(string path, string key){}
class HttpContext{
    string path;
    JSON(int code, any v){
        string json = JSON(v);
        FprintfJSON(code, path, json);
    }
    HTML(int code, any v) {
        string html = v;
        FprintfHTML(code, path, html);
    }
    string queryPath() {
        string p = QueryPath(path);
        return p;
    }

    string formValue(string key){
        string v = FormValue(path, key);
        return v;
    }
}
// Bind route
httpHandle(string method, string path, func (HttpContext) handle){
    // println("path="+path);
    HttpContext ctx = HttpContext();
    handle(ctx);
}
// Run http server.
httpRun(string addr){}
```

å¯åŠ¨ä¸€ä¸ª `HTTP` æœåŠ¡ï¼š

```js
class Person{
    string name;
}
func (HttpContext) handle (HttpContext ctx){
    Person p = Person();
    p.name = "abc";
    println("p.name=" + p.name);
    println("ctx=" + ctx);
    ctx.JSON(200, p);
}

func (HttpContext) handle1 (HttpContext ctx){
    Person p = Person();
    p.name = "def";
    println("p.name=" + p.name);
    println("ctx=" + ctx);
    ctx.JSON(200, p);
}
func (HttpContext) handle2 (HttpContext ctx){
    DateTime d = DateTime();
    string local = d.getCurrentTime("Asia/Shanghai","2006-01-02 15:04:05");
    println(local);
    string html =^
    <html>
    <title>hello</title>
    <h1>current ^+ local +^</h1>
    <p>hahaha</p>
    </html>
    ^;
    string queryPath = ctx.queryPath();
    println("queryPath = " + queryPath);

    // http://127.0.0.1:8000/p/2?id=100
    string id = ctx.formValue("id");
    println("id="+id);
    ctx.HTML(200, html);
}
httpHandle("get", "/p", handle);
httpHandle("get", "/p/1", handle1);
httpHandle("get", "/p/2", handle2);
httpRun(":8000");

```



## è”ç³»ä½œè€…


> crossoverJie#gmail.com

![qrcode_for_gh_3a954a025f10_258.jpg](https://i.loli.net/2019/07/09/5d245f3e955ce61699.jpg) 